
buildscript {
  repositories {
    jcenter()
      google()
  }
  dependencies {
    //noinspection GradleDependency
    classpath 'com.android.tools.build:gradle:3.1.4'
    classpath 'de.undercouch:gradle-download-task:3.1.2'
  }
}

import org.apache.tools.ant.taskdefs.condition.Os
import de.undercouch.gradle.tasks.download.Download

apply plugin: 'de.undercouch.download'
apply plugin: 'com.android.library'

def downloadsDir = new File("$buildDir/downloads")
def thirdPartyNdkDir = new File("$buildDir/third-party-ndk")

task createNativeDepsDirectories {
  downloadsDir.mkdirs()
  thirdPartyNdkDir.mkdirs()
}

// Custom task for NDK module

@SuppressWarnings("GrMethodMayBeStatic")
def getNdkBuildName() {
  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    return "ndk-build.cmd"
  } else {
    return "ndk-build"
  }
}

def findNdkBuildFullPath() {
  // we allow to provide full path to ndk-build tool
  if (hasProperty('ndk.command')) {
    return property('ndk.command')
  }
  // or just a path to the containing directory
  if (hasProperty('ndk.path')) {
    def ndkDir = property('ndk.path')
    return new File(ndkDir, getNdkBuildName()).getAbsolutePath()
  }
  if (System.getenv('ANDROID_NDK') != null) {
    def ndkDir = System.getenv('ANDROID_NDK')
    return new File(ndkDir, getNdkBuildName()).getAbsolutePath()
  }
  def ndkDir = android.hasProperty('plugin') ? android.plugin.ndkFolder :
  project.android.ndkDirectory ? project.android.ndkDirectory.absolutePath : plugins.getPlugin('com.android.library').sdkHandler.getNdkFolder()

  if (ndkDir) {
    return new File(ndkDir, getNdkBuildName()).getAbsolutePath()
  }
  return null
}

def getNdkBuildFullPath() {
  def ndkBuildFullPath = findNdkBuildFullPath()
  if (ndkBuildFullPath == null) {
    throw new GradleScriptException(
            "ndk-build binary cannot be found, check if you've set " +
                    "\$ANDROID_NDK environment variable correctly or if ndk.dir is " +
                    "setup in local.properties",
            null)
  }
  if (!new File(ndkBuildFullPath).canExecute()) {
    throw new GradleScriptException(
            "ndk-build binary " + ndkBuildFullPath + " doesn't exist or isn't executable.\n" +
                    "Check that the \$ANDROID_NDK environment variable, or ndk.dir in local.proerties, is set correctly.\n" +
                    "(On Windows, make sure you escape backslashes in local.properties or use forward slashes, e.g. C:\\\\ndk or C:/ndk rather than C:\\ndk)",
            null)
  }
  return ndkBuildFullPath
}

task downloadJSCHeaders(dependsOn: createNativeDepsDirectories, type: Download) {
  // in sync with webkit SVN revision 174650
  def jscAPIBaseURL = 'https://raw.githubusercontent.com/WebKit/webkit/38b15a3ba3c1b0798f2036f7cea36ffdc096202e/Source/JavaScriptCore/API/'
  def jscHeaderFiles = ['JavaScript.h', 'JSBase.h', 'JSContextRef.h', 'JSObjectRef.h', 'JSRetainPtr.h', 'JSStringRef.h', 'JSValueRef.h', 'WebKitAvailability.h']
  def output = new File(downloadsDir, 'jsc')
  output.mkdirs()
  src(jscHeaderFiles.collect { headerName -> "$jscAPIBaseURL$headerName" })
  onlyIfNewer true
  overwrite false
  dest output
}

task prepareJSC(dependsOn: downloadJSCHeaders) << {
  copy {
    from {downloadJSCHeaders.dest}
    include 'jni/**/*.so', '*.h', 'Android.mk'
    //noinspection SpellCheckingInspection
    filesMatching('*.h', { fname -> fname.path = "JavaScriptCore/${fname.path}"})
    into "$thirdPartyNdkDir/jsc"
  }
}

task buildRNWebGL2Lib(dependsOn: [prepareJSC], type: Exec) {
  inputs.dir('src/main/jni')
  inputs.dir('../cpp')
  outputs.dir("$buildDir/rnwebgl2/all")
  commandLine getNdkBuildFullPath(),
          'NDK_PROJECT_PATH=null',
          "NDK_APPLICATION_MK=$projectDir/src/main/jni/Application.mk",
          'NDK_OUT=' + temporaryDir,
          "NDK_LIBS_OUT=$buildDir/rnwebgl2/all",
          "THIRD_PARTY_NDK_DIR=$thirdPartyNdkDir",
          '-C', file('src/main/jni').absolutePath,
          '--jobs', Runtime.runtime.availableProcessors()
}

task cleanRNWebGL2Lib(type: Exec) {
  commandLine getNdkBuildFullPath(),
          '-C', file('src/main/jni').absolutePath,
          'clean'
}

task packageRNWebGL2Libs(dependsOn: buildRNWebGL2Lib, type: Copy) {
  from "$buildDir/rnwebgl2/all"
  exclude "**/libjsc.so"
  exclude "**/libc++_shared.so"
  exclude "**/libgnustl_shared.so"
  into "$buildDir/rnwebgl2/exported"
}

android {
  compileSdkVersion 27
  buildToolsVersion '27.0.3'

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 27
    versionCode 1
    versionName "1.0"
    ndk {
      abiFilters 'armeabi-v7a', 'x86'
    }
    // Use custom task for NDK module from above
    sourceSets.main {
      jni.srcDirs = []
      jniLibs.srcDir "$buildDir/rnwebgl2/exported"
    }
    tasks.withType(JavaCompile) {
      compileTask -> compileTask.dependsOn packageRNWebGL2Libs
    }
    clean.dependsOn cleanRNWebGL2Lib
  }
}

repositories {
  mavenCentral()
}

dependencies {
  //noinspection GradleDynamicVersion
  compile "com.facebook.react:react-native:+"
}